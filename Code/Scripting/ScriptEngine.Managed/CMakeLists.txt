ez_cmake_init()

ez_requires_mono()

set(EZ_MANAGED_ASSEMBLY_NAME "ezEngine.Managed")
set(EZ_MANAGED_CSPROJ ${EZ_MANAGED_ASSEMBLY_NAME}.csproj)

# Get the name of this folder as the project name
# get_filename_component(PROJECT_NAME ${CMAKE_CURRENT_SOURCE_DIR} NAME_WE)

set(EZ_MANAGED_ASSEMBLY_EXCLUDE_FILES "")
set(EXCLUDE_FILES CMakeLists.txt Build.props.in ${EZ_MANAGED_CSPROJ}.in)

foreach(EXCLUDE_FILE ${EXCLUDE_FILES})
  string(APPEND EZ_MANAGED_ASSEMBLY_EXCLUDE_FILES "    <None Remove=\"${EXCLUDE_FILE}\" />\n")
endforeach()


set(EZ_MANAGED_ASSEMBLY_COMPILE_FILES "")
file(GLOB_RECURSE CSHARP_FILES "*.cs")
foreach(CSHARP_FILE ${CSHARP_FILES})
  string(APPEND EZ_MANAGED_ASSEMBLY_COMPILE_FILES "    <Compile Include=\"${CSHARP_FILE}\" />\n")
endforeach()

if (${CMAKE_BUILD_TYPE} STREQUAL "Debug")
	set(EZ_MANAGED_PROJECT_CONFIG Debug)
elseif (${CMAKE_BUILD_TYPE} STREQUAL "Release")
	set(EZ_MANAGED_PROJECT_CONFIG Release)
elseif (${CMAKE_BUILD_TYPE} STREQUAL "MinSizeRel")
	set(EZ_MANAGED_PROJECT_CONFIG Release)
elseif (${CMAKE_BUILD_TYPE} STREQUAL "RelWithDebInfo")
	set(EZ_MANAGED_PROJECT_CONFIG Checked)
endif()	

if(${EZ_CMAKE_ARCHITECTURE_POSTFIX} STREQUAL "Arm32")
  set(EZ_MANAGED_PROJECT_PLATFORMTARGET "arm")
elseif(${EZ_CMAKE_ARCHITECTURE_POSTFIX} STREQUAL "Arm64")
  set(EZ_MANAGED_PROJECT_PLATFORMTARGET "arm64")
elseif(${EZ_CMAKE_ARCHITECTURE_POSTFIX} STREQUAL "32")
  set(EZ_MANAGED_PROJECT_PLATFORMTARGET "x86")
elseif(${EZ_CMAKE_ARCHITECTURE_POSTFIX} STREQUAL "64")
  set(EZ_MANAGED_PROJECT_PLATFORMTARGET "x64")
endif()

set(MANAGED_PROJECT_DIR ${CMAKE_CURRENT_BINARY_DIR}/${EZ_MANAGED_PROJECT_CONFIG}/${EZ_MANAGED_PROJECT_PLATFORMTARGET})

configure_file(${CMAKE_CURRENT_SOURCE_DIR}/${EZ_MANAGED_CSPROJ}.in ${MANAGED_PROJECT_DIR}/${EZ_MANAGED_CSPROJ})

if(MSVC)
  include_external_msproject(${EZ_MANAGED_ASSEMBLY_NAME} ${MANAGED_PROJECT_DIR}/${EZ_MANAGED_CSPROJ})
  set_property(TARGET ${EZ_MANAGED_ASSEMBLY_NAME} PROPERTY FOLDER Scripting)
else()
 # todo
endif()

ez_set_default_target_output_dirs(${EZ_MANAGED_ASSEMBLY_NAME})

get_target_property(OUTPUT_DIR_DEBUG ${EZ_MANAGED_ASSEMBLY_NAME} RUNTIME_OUTPUT_DIRECTORY_DEBUG)
get_target_property(OUTPUT_DIR_RELEASE ${EZ_MANAGED_ASSEMBLY_NAME} RUNTIME_OUTPUT_DIRECTORY_RELEASE)
get_target_property(OUTPUT_DIR_CHECKED ${EZ_MANAGED_ASSEMBLY_NAME} RUNTIME_OUTPUT_DIRECTORY_RELWITHDEBINFO)

set(EZ_MANAGED_ASSEMBLY_OUTPUT_DEBUG ${OUTPUT_DIR_DEBUG}/Assemblies)
set(EZ_MANAGED_ASSEMBLY_OUTPUT_RELEASE ${OUTPUT_DIR_RELEASE}/Assemblies)
set(EZ_MANAGED_ASSEMBLY_OUTPUT_CHECKED ${OUTPUT_DIR_CHECKED}/Assemblies)

configure_file(${CMAKE_CURRENT_SOURCE_DIR}/Build.props.in ${MANAGED_PROJECT_DIR}/Build.props)